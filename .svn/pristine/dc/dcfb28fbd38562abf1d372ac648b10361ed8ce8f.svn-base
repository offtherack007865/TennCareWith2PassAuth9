-- SQL Server Instance:  smg-sql01
USE [Utilities];
IF EXISTS (SELECT * FROM sys.objects WHERE type = 'P' AND OBJECT_ID = OBJECT_ID('tc.du_MarkMonthlyEntryLookedUpTodayAsLookedUp'))
   DROP PROC [tc].[du_MarkMonthlyEntryLookedUpTodayAsLookedUp]
GO


SET ANSI_NULLS ON
GO
SET QUOTED_IDENTIFIER ON
GO
                       
CREATE PROCEDURE [tc].[du_MarkMonthlyEntryLookedUpTodayAsLookedUp]
(
  @inputMonthlyEntriesLookedUpTodayID [int]
)
/* -----------------------------------------------------------------------------------------------------------
   Procedure Name  :  tc.du_MarkMonthlyEntryLookedUpTodayAsLookedUp
   Business Analyis:
   Project/Process :   
   Description     :  Mark Monthly Entry as Looked-up.
	  
   Author          :  Philip Morrison 
   Create Date     :  04/18/2025

   ***********************************************************************************************************
   **         Change History                                                                                **
   ***********************************************************************************************************

   Date       Version    Author             Description
   --------   --------   -----------        ------------
   04/18/2025 1.01.001   Philip Morrison    Created

*/ -----------------------------------------------------------------------------------------------------------                                      
AS
BEGIN
-- Declarations
DECLARE @ApplicationID              int          = 0

DECLARE @Application            varchar(128) = 'TennCareEligility' 

SELECT @ApplicationID = [ApplicationID]
FROM [ADMIN].[Administration].[admin].[Application] 
WHERE [ApplicationName] = @Application


DECLARE @Version                varchar(25)  = '1.00.001'

DECLARE @ProcessID              int          = 0
DECLARE @Process                varchar(128) = 'TennCareEligilityImport'

SELECT @ProcessID = [ProcessID]
FROM [ADMIN].[Administration].[support].[Process]
WHERE [ApplicationID] = @ApplicationID
AND [Name] = @Process

DECLARE @BatchOutID             int
DECLARE @BatchDescription       varchar(1000) = @@ServerName + '  - ' + @Version
DECLARE @BatchDetailDescription varchar(1000)
DECLARE @BatchMessage           varchar(MAX)
DECLARE @User                   varchar(128) = SUSER_NAME()

DECLARE @AnticipatedRecordCount int 
DECLARE @ActualRecordCount      int

SET NOCOUNT ON

BEGIN TRY

--  Initialize Batch
    EXEC Admin.Utilities.logs.di_Batch @BatchOutID OUTPUT,  NULL, 'BatchStart', @BatchDescription, @ProcessID, @Process
----------------------------------------------------------------------------------------------------------------------------------------------------

    SET @BatchDetailDescription = 'Update Given Monthly Entry as Looked-Up'
    EXEC Admin.Utilities.logs.di_Batch @BatchOutID OUTPUT,  @BatchOutID, 'DetailStart', @BatchDetailDescription
	
	  SELECT @AnticipatedRecordCount = COUNT(*)
	                                   FROM [TennCareEligibility].[tc].[MonthlyEntriesLookedUpToday]
									   WHERE [MonthlyEntriesLookedUpTodayID] = @inputMonthlyEntriesLookedUpTodayID;
	  
    -- Update Given Monthly Entry as Looked-Up
    UPDATE [TennCareEligibility].[tc].[MonthlyEntriesLookedUpToday]
	SET [LookedUp] = 1
	WHERE [MonthlyEntriesLookedUpTodayID] = @inputMonthlyEntriesLookedUpTodayID;
	
    SET @ActualRecordCount = @@ROWCOUNT
    EXEC Admin.Utilities.logs.di_Batch @BatchOutID OUTPUT,  @BatchOutID, 'DetailEnd', NULL, NULL, NULL, @AnticipatedRecordCount, @ActualRecordCount

----------------------------------------------------------------------------------------------------------------------------------------------------
    SET @BatchDetailDescription = 'Update Given Monthly Entry History as Looked-Up'
    EXEC Admin.Utilities.logs.di_Batch @BatchOutID OUTPUT,  @BatchOutID, 'DetailStart', @BatchDetailDescription

      	
	
	  SELECT @AnticipatedRecordCount = COUNT(*)
	                                   FROM [TennCareEligibility].[tc].[MonthlyEntriesLookedUpToday] today
	                                   JOIN [TennCareEligibility].[tc].[MonthlyEntriesLookedUpTodayHistory] hist
	                                   ON hist.[FileName] = today.[FileName]
	                                      AND hist.[PatientID] = today.[PatientID]
		                                  AND hist.[DOS] = today.[DOS] 
	                                   WHERE today.[MonthlyEntriesLookedUpTodayID] = @inputMonthlyEntriesLookedUpTodayID;
	  
    -- Update Given Monthly Entry History as Looked-Up
    UPDATE [TennCareEligibility].[tc].[MonthlyEntriesLookedUpTodayHistory]
	SET [LookedUp] = 1
	FROM [TennCareEligibility].[tc].[MonthlyEntriesLookedUpToday] today
	JOIN [TennCareEligibility].[tc].[MonthlyEntriesLookedUpTodayHistory] hist
	  ON hist.[FileName] = today.[FileName]
	     AND hist.[PatientID] = today.[PatientID]
		 AND hist.[DOS] = today.[DOS] 
	WHERE today.[MonthlyEntriesLookedUpTodayID] = @inputMonthlyEntriesLookedUpTodayID;
	
    SET @ActualRecordCount = @@ROWCOUNT
    EXEC Admin.Utilities.logs.di_Batch @BatchOutID OUTPUT,  @BatchOutID, 'DetailEnd', NULL, NULL, NULL, @AnticipatedRecordCount, @ActualRecordCount

-------------------------------------------------------------------------------
--  Close batch
    EXEC Admin.Utilities.logs.di_batch @BatchOutID OUTPUT, @BatchOutID, 'BatchEnd'

END TRY

BEGIN CATCH
DECLARE @Err              int
     ,  @ErrorMessage     varchar(Max)
     ,  @ErrorLine        varchar(128)
     ,  @Workstation      varchar(128) = @Application
     ,  @Procedure        VARCHAR(500)

    IF ERROR_NUMBER() IS NULL 
      SET @Err =0;
    ELSE
      SET @Err = ERROR_NUMBER();

    SET @ErrorMessage = ERROR_MESSAGE()
    SET @ErrorLine    = 'SP Line Number: ' + CAST(ERROR_LINE() as varchar(10)) 
    SET @Workstation  = HOST_NAME()
    SET @Procedure    = @@SERVERNAME + '.' + DB_NAME() + '.' + OBJECT_SCHEMA_NAME(@@ProcID) + '.' + OBJECT_NAME(@@ProcID) + ' - ' + @ErrorLine + ' - ' + LEFT(@BatchDetailDescription, 7)

    EXEC Admin.Utilities.administration.di_ErrorLog  @Application ,@Process, @Version ,@Err, @ErrorMessage, @Procedure,  @User , @Workstation

    SET @BatchMessage = 'Process Failed:  ' +  @ErrorMessage
    EXEC Admin.Utilities.logs.di_batch @BatchOutID OUTPUT, @BatchOutID, 'BatchEnd', @BatchMessage
    RAISERROR(@ErrorMessage, 16,1)
END CATCH


declare @IsOk [bit] 
declare @MyCount [int] 

set @MyCount = 0
select @MyCount = count(*)
  from [TennCareEligibility].[tc].[MonthlyEntriesLookedUpToday]
 where [MonthlyEntriesLookedUpTodayID] = @inputMonthlyEntriesLookedUpTodayID
   and [LookedUp] = 1; 	

if (@MyCount > 0) begin
  set @IsOk = 1
  select @IsOk as [IsOk]
  return
end  

set @IsOk = 0
select @IsOk as [IsOk]

END



  
